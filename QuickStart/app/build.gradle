apply plugin: 'com.android.application'

repositories {
    flatDir {
        dirs '../../../sdk' //location of bbm_sdk aar file
    }
    google()
    jcenter()
}

/*
 * Copyright (c) 2017 BlackBerry.  All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Create a variable called appPropertiesFile, and initialize it to your
// app.properties file, in the rootProject folder.
def appPropertiesFile = rootProject.file("app.properties")
def appProperties = new Properties()
appProperties.load(new FileInputStream(appPropertiesFile))


android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.bbm.example.quickstart"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // The Server Client ID is a Web OAuth 2.0 Client ID obtained from https://console.developers.google.com/.
        // For the purpose of this example app we add it here, but in a real world environment this string should be protected.
        buildConfigField "String", "CLIENT_SERVER_ID", appProperties['client_server_id']

        // The BBM Enterprise User Domain that is assigned to you.
        // For the purpose of this example app we add it here, but in a real world environment this string should be protected.
        resValue "string", "USER_DOMAIN", appProperties['user_domain']
    }

    signingConfigs {
        common {
            keyAlias appProperties['keyAlias']
            keyPassword appProperties['keyPassword']
            storeFile file(appProperties['storeFile'])
            storePassword appProperties['storePassword']
        }
    }

    buildTypes {
        debug{
            debuggable true
            signingConfig signingConfigs.common
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.common
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:26.0.2'

    // Include Google play services auth module to use Open ID.
    implementation 'com.google.android.gms:play-services-auth:11.4.2'

    // BBM Enterprise SDK used Guva and is required.
    implementation 'com.google.guava:guava:22.0-android'

    // Include the dependency on the BBM Enterprise SDK
    implementation (name:'bbm_sdk-release', ext:'aar')
}

// By applying the plugin below this will ensure the project will not
// if google-services.json is missing. When setting up this example please
// ensure you follow https://developers.google.com/identity/sign-in/android/start
// and download the resulting google-services.json file.
apply plugin: 'com.google.gms.google-services'
